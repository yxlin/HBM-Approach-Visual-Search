options  			  # This programme is modified from Stoet's (2011) visual search task 
 #window  			  # 
 escape  			  # 
 centerzero 			  # 
 bitmapdir bitmapssearch 	  # 
 resolution 1152 864              # 1152x864 is SONY's resolution with 100 Hz RF.  		      
 parallelport out 2 3 4 5 6 7 8 9 # allow set parallel port to send out high voltage 
 vsync_off                        # unresolved issue
 cedrus				  # Author: Yi-Shin Lin
                                  
bitmaps 			 
 instruction		
 prcFullsetCompleted		
 takeABreak			 
 lastOne
 finish3Sections
 finish5Sections
 finish7Sections
 finish9Sections
 target
 distractor1
 distractor2
 empty
 fixpoint
 frame
 tooearly
 toolate

table feature # This table is created in search.R and searchTabFun.R

task search
     table feature
     c-begin
	 set_pin(LP_PIN02);
	 psy_show_centered_bitmap(fixpoint, 0, 0, 1);  // image1
      	 psy_delay(500);
	 set_pin(LP_PIN03);
	 psy_show_centered_bitmap(empty, 0, -30, 1);   // image2
     	 psy_delay(200);                                                // the cue bitmap stays on the screen for 200 ms.
	 psy_delay(feature[tablerow].c4);				// The duration of cue-offset to target-onset is @4, defined at isiSet in R file.
     c-end
     cedrus clear                                  # clean any cedrus guess press before presenting the target set 
     draw off 
         show bitmap @4 @5 @6 	# 3; this is the target; show bitmap [bitmapname] (xpos ypos)(width height)
	 show bitmap @7 @8 @9 	# 4
	 show bitmap @10 @11 @12 	# 5
	 show bitmap @13 @14 @15 	# 6
	 show bitmap @16 @17 @18 	# 7
	 show bitmap @19 @20 @21 	# 8
	 show bitmap @22 @23 @24 	# 9
	 show bitmap @25 @26 @27 	# 10
	 show bitmap @28 @29 @30 	# 11
	 show bitmap @31 @32 @33 	# 12
         show bitmap @34 @35 @36 	# 13  
	 show bitmap @37 @38 @39 	# 14
	 show bitmap @40 @41 @42 	# 15
	 show bitmap @43 @44 @45 	# 16
	 show bitmap @46 @47 @48 	# 17
	 show bitmap @49 @50 @51 	# 18
	 show bitmap @52 @53 @54 	# 19
	 show bitmap @55 @56 @57 	# 20
	 show bitmap @58 @59 @60 	# 21
	 show bitmap @61 @62 @63 	# 22
	 c-begin
	    set_pin(LP_PIN04);
 	 c-end
  draw on
  c-begin
  /*  Record RT, using Cedrus RB-830. If longer than 2 seconds, terminate the trial. */
  keystatus = psy_cedrus_readkey(feature[tablerow].c2, 4000);
  set_pin(LP_PIN05);
  /*--------------------------------------------------------------------------------------------------------*/ 
  psy_clear_screen();
  /* Reset all parallel pin back to 0 voltage. This defines the trial finish event */
  clear_pin(LP_PIN02 | LP_PIN03 | LP_PIN04 | LP_PIN05 | LP_PIN06 | LP_PIN07 | LP_PIN08 | LP_PIN09);
  psy_delay(800);  
  /*   c1-counter(); c2-target present(present-1/absent-2); c3-setsize; 
       keystatus.key records which cedrus key (7 or 1) was pressed; 
       keystatus.status records correct, incorrect, too early or timeout; 
       keystatus.externaltime1 (EXTRT) record RT from the Cedrus key pad 
  */
  fprintf(datafile, "%s %d %d %d %d %d %d \n", blockname,
	  feature[tablerow].c1, feature[tablerow].c2,
	  feature[tablerow].c3, keystatus.key + 1, keystatus.status,
	  keystatus.externaltime1);
  c-end
  
#----------------------------------------------------------------------
# blocks
# ---------------------------------------------------------------------

block prc			
  pager instruction 	   
  delay 1500                    
  tasklist 	  		 
    search 8 fixed 	  
  end
  system R CMD BATCH dataDump.r

message prcFullsetCompleted

block feature1			
  delay 1500                    
  tasklist 	  		 
    search 80 fixed 	 
  end
  system R CMD BATCH dataDump.r

message takeABreak

block feature2			
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r

message takeABreak

block feature3	 	
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r

message finish3Sections

block feature4	 		 
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r
  
message takeABreak

block feature5	 	 
  delay 1500
  tasklist 
    search 80 fixed 
  end

message finish5Sections

block feature6	 	
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r
  
message takeABreak

block feature7	 	
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r

message finish7Sections

block feature8	 		
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r

message takeABreak

block feature9	 		
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r

message lastOne

block feature10	 		
  delay 1500
  tasklist 
    search 80 fixed 
  end
  system R CMD BATCH dataDump.r
  bitmap_from_file ./bitmapssearch/completed.png
  wait_for_key

 

